apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 40203
        versionName "4.2.3" /* Update in BoxConfig.SDK_VERSION as well */
        version "4.2.3"
        group "com.box"
        
        // Set version code and name
        def env = System.getenv()
        def git_count = env['GIT_COUNT'] as Integer
        if (git_count != null) {
        /* Update in BoxConfig.SDK_VERSION as well */
            versionCode git_count + 40000
            versionName "4.99." + git_count + "-SNAPSHOT"
            version "4.99." + git_count + "-SNAPSHOT"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize = "4g"
    }
}

dependencies {
    implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.1'
    implementation 'androidx.annotation:annotation:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.powermock:powermock-module-junit4:1.6.2"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.6.2"
    testImplementation "org.powermock:powermock-api-mockito:1.6.2"
    testImplementation "org.powermock:powermock-classloading-xstream:1.6.2"
}

apply from: '../maven_push.gradle'
